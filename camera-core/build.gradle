apply from: configs.androidLibrary
apply plugin: 'maven-publish'

dependencies {
    implementation project(":stripe-core")
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintlayoutVersion"

    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.annotation:annotation:$androidxAnnotationVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"

    def camerax_version = "1.2.1"

    implementation "androidx.camera:camera-core:$camerax_version"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:$camerax_version"

    testImplementation "androidx.test:core:$androidTestVersion"
    testImplementation "androidx.test:runner:$androidTestRunnerVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion"
    testImplementation "org.mockito:mockito-core:$mockitoCoreVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "com.google.truth:truth:$truthVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"

    androidTestImplementation "androidx.test:rules:$androidTestVersion"
    androidTestImplementation "androidx.test:runner:$androidTestRunnerVersion"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion"
    androidTestUtil "androidx.test:orchestrator:$androidTestOrchestratorVersion"

//    ktlint "com.pinterest:ktlint:$ktlintVersion"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.wizdome' // Replace with your desired groupId
            artifactId 'camera-core' // Replace with your desired artifactId
            version '20.17.0' // Replace with your desired version

            // Specify the artifact generated by the Android build (AAR file)
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

            // Include the library's dependencies in the POM
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                // Include api, implementation, and project dependencies
                (configurations.api.allDependencies + configurations.implementation.allDependencies).each { dependency ->
                    // Skip project dependencies since they are included in the build
                    if (dependency instanceof ProjectDependency) {
                        return
                    }
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

tasks.named("publishMavenPublicationToMavenLocal").configure {
    dependsOn("assembleRelease")
}

ext {
    artifactId = "camera-core"
    artifactName = "camera-core"
    artifactDescrption = "The camera core module of Stripe Android SDK"
}

apply from: "${rootDir}/deploy/deploy.gradle"
